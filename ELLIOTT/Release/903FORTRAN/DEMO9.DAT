C
C SOLVE QUADRATIC EQUATIONS (using LG 903 FORTRAN)
C

C Load Fortran Load-and-Go System
on 903 16K
telecode 903
nonprinting off
select out tty

at ptr file 903fort16klg(iss5).bin
j 8181
st 8171

at ptr inline 903

      GLOBAL, QUAD, SOLVE, OUTPUT]
C  BASED ON CASE STUDY 13 (QUADRATIC EQUATION SOLUTION WITH
C  SUBPROGRAMS) IN D.D. MCCRACKEN "A GUIDE TO FORTRAN IV PROGRAMMING".

C  MAIN PROGRAM

      COMMON X1REAL, X1IMAG, X2REAL, X2IMAG

C     ...USE DO TO COUNT PAGES AND SET LIMIT ON OUTPUT
      DO 40 NPAGE = 1, 100

C        ...WRITE HEADING AT TOP OF NOMINAL PAGE
         WRITE (2, 10) NPAGE
   10    FORMAT (9X, 1HA, 14X, 1HB, 14X, 1HC, 11X, 7HX1 REAL, 8X,
£                7HX1 IMAG, 8X, 7HX2 REAL, 8X, 7HX2 IMAG, 7X, 4HPAGE, I4//)
         

C        ...USE ANOTHER DO TO COUNT LINES
         DO 30 LINE = 1, 10

C           ...READ A NEW RECORD
            READ (1) A1, B1, C1, A2, B2, C2

C           ...CHECK FOR SENTINEL
            IF (A1) 20, 999, 20

C           ...SOLVE BOTH EQUATIONS AND PRINT RESULTS
   20       CALL SOLVE  (A1, B1, C1)
            CALL OUTPUT (A1, B1, C1)
            CALL SOLVE  (A2, B2, C2)
            CALL OUTPUT (A2, B2, C2)
   30    CONTINUE
   40 CONTINUE

  999 STOP
      END

C  SUBROUTINE FOR FINDING ROOTS

      GLOBAL, SOLVE, SQRT ]
      SUBROUTINE SOLVE (A, B, C)
      COMMON X1REAL, X1IMAG, X2REAL, X2IMAG

C     ...CALCULATE DISCRIMINANT
      DISC = B*B - 4.0*A*C

C     ...TEST FOR REAL OR COMPLEX ROOTS
      IF (DISC) 10, 20, 30

C     ...COMPLEX ROOTS
   10 X1REAL = -B / (2.0*A)
      X2REAL = X1REAL
      X1IMAG = SQRT(-DISC) / (2.0*A)
      X2IMAG = -X1IMAG
      RETURN

C     ...REAL AND EQUAL ROOTS
   20 X1REAL = -B / (2.0*A)
      X2REAL = X1REAL
      X1IMAG = 0.0
      X2IMAG = 0.0
      RETURN

C     ...REAL AND UNEQUAL ROOTS
   30 S = SQRT(DISC)
      X1REAL = (-B+S) / (2.0*A)
      X2REAL = (-B-S) / (2.0*A)
      X1IMAG = 0.0
      X2IMAG = 0.0
      RETURN
      END

C  SUBROUTINE FOR PRINTING RESULTS

      GLOBAL, OUTPUT]
      SUBROUTINE OUTPUT (A, B, C)
      COMMON X1REAL, X1IMAG, X2REAL, X2IMAG

C     ...CHECK IF ROOTS ARE PURE REAL
      IF (X1IMAG) 20, 10, 20
   10 WRITE  (2, 90) A, B, C, X1REAL, X2REAL
   90 FORMAT (4E15.4, 15X, E15.4)
      RETURN

C     ...CHECK ROOTS ARE PURE IMAGINARY
   20 IF (X1REAL) 40, 30, 40
   30 WRITE  (2, 91) A, B, C, X1IMAG, X2IMAG
   91 FORMAT (3E15.4, 15X, E15.4, 15X, E15.4)
      RETURN

C     ...MIXED ROOTS
   40 WRITE  (2, 93) A, B, C, X1REAL, X1IMAG, X2REAL, X2IMAG
   93 FORMAT (7E15.4)
      RETURN
      END


<! Halt !>
<!!>

C translate - compile
J 8

C indicate complete
j 10

c run
at ptr inline

1 	-2 	1 	1 	-10 	25
1 	-3 	2 	2 	-6 	4
1	1	-2550	10	-20	10
1000	-2000	1000	0.02 	-0.04	0.02
1.432	9.876	-4.567	8.813	-1.31	0.0
2.3003	1.9917 	0.0	1.0	0.0	-1.0
1.0	0.0	1.0	9.0	0.0	36.0
1	2	5	535	22	1583
-9	23	37	61	2	87
61	159	87	100	99	98
0	0	0	0	0	0


 

<! Halt !>
<!!>
j 11

sh t




